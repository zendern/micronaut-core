plugins {
    id 'java-platform'
}

group projectGroupId

apply plugin: 'maven-publish'

def profiles = new Properties()
profiles.load(new StringReader(new File("$projectDir/profiles.properties").text))
boolean micronautSnapshot = rootProject.version.toString().endsWith("-SNAPSHOT")

dependencies {
    constraints {
        for (profile in profiles) {
            if (!isBuildSnapshot && version.toString().endsWith("-SNAPSHOT")) {
                throw new RuntimeException("Cannot have a snapshot dependency on a profile [$profile.key] for a release!")
            }
            api "io.micronaut.profiles:$profile.key:$profile.value"
        }

        for (Project p : rootProject.subprojects) {
            if (!p.subprojects.empty) continue
            if (p.name == "bom") continue
            if (p.hasProperty("bomExclude") && p.property("bomExclude") == true) continue

            api project(":$p.name")
        }

        for (dep in dependencyVersions) {
            def info = dep.value
            // don't include snapshots
            if (info.version.toString().endsWith("-SNAPSHOT") && !micronautSnapshot) {
                continue
            }

            def versionExpr = dep.value.version

            if (info.name) {
                api "$info.group:$info.name:$versionExpr"
            }
            if (info.modules) {
                for (m in info.modules) {
                    api "$info.group:$m:$versionExpr"
                }
            }
        }
    }
}

publishing {
    publications {
        myPlatform(MavenPublication) {
            artifactId "micronaut-${project.name}"
            from components.javaPlatform
        }
    }
}